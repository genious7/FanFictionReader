buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'io.fabric'

android {
    compileSdkVersion 25

    defaultConfig {
        applicationId "com.spicymango.fanfictionreader"
        minSdkVersion 9
        targetSdkVersion 25
        versionCode 87
        versionName "1.57b"
    }
    lintOptions {
        abortOnError true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        release
    }
    buildTypes {
        debug{
            applicationIdSuffix ".debug"
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.pro'
            signingConfig signingConfigs.release
        }
    }
    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
    }
}

dependencies {
    implementation project(':kolavar')
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.7'
    implementation 'com.android.support:appcompat-v7:25.4.0'
    implementation 'com.android.support:design:25.4.0'
    implementation 'org.jsoup:jsoup:1.11.2'
    implementation 'org.apache.commons:commons-collections4:4.1'
    implementation 'org.apache.commons:commons-lang3:3.7'
    implementation ('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true
    }
}

// If the signing configurations are defined in the local gradle.properties, add them to the script
if (project.hasProperty('key.alias')) {
    android.signingConfigs.release.keyAlias = project.property('key.alias')
    android.signingConfigs.release.keyPassword = project.property('key.alias.password')
    android.signingConfigs.release.storeFile = file(project.property('key.store'))
    android.signingConfigs.release.storePassword = project.property('key.store.password')
}
